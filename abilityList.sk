function setAbility(p: player, sn: number, ab: text, b: boolean = false):
    loop {-ab.%{_ab}%.effects::*}:
        if {_ef} is not set:
            set {_ef} to "&7效果： %loop-value%"
        else:
            set {_ef} to "%{_ef}%||&7      %loop-value%"
    if {_b} is false:
        set {_name} to "&7［能力］ %{-ab.%{_ab}%::name}%"
    else:
        set {_name} to "&7［基礎］ %{-ab.%{_ab}%::name}%"
    set {_cast} to "&7發動： %{-ab.%{_ab}%::cast}%"
    set {_cd} to "&7冷卻： %{-ab.%{_ab}%::cd}%"
    set slot {_sn} of {_p}'s current inventory to {-ab.%{_ab}%.sign} named {_name} with lore "%{-ab.%{_ab}%::perm}%||%{_cast}%||%{_ef}%||%{_cd}%"

function setAbList(lname: text, p: player, hideOwn: boolean = false):
    open "CHEST" with 2 rows named {_lname} to {_p}
    invalidSlots((2, 4, 6, 10, 13 and 16), {_p})
    set {_abPos::*} to (0, 1, 3, 5, 7, 8, 9, 11, 12, 14, 15 and 17)
    loop {-abList::*}:
        if {_hideOwn} is true:
            if {_p} has permission "ability.%loop-value%":
                invalidSlot({_abPos::%loop-index%}, {_p})
            else:
                setAbility({_p}, {_abPos::%loop-index%}, loop-value, false)
        else:
            setAbility({_p}, {_abPos::%loop-index%}, loop-value, false)

function viewOwnAb(lname: text, p: player, basic: boolean):
    if {_basic} is true:
        loop {-abBasicList::*}:
            add loop-value to {_pabOwn::*}
        set {_bl} to amount of {-abBasicList::*}
    else:
        set {_bl} to 0
    loop {-abList::*}:
        if {_p} has permission "ability.%loop-value%":
            add loop-value to {_pabOwn::*}
    set {_row} to 1
    if amount of {_pabOwn::*} > 9:
        add 1 to {_row}
    open "CHEST" with {_row} row named {_lname} to {_p}
    set {_i} to 0
    loop {_pabOwn::*}:
        if {_i} < {_bl}:
            set {_sb} to true
        else:
            set {_sb} to false
        setAbility({_p}, {_i}, loop-value, {_sb})
        add 1 to {_i}
    while {_i} <= {_row}*9-1:
        invalidSlot({_i}, {_p})
        add 1 to {_i}

function abInit(name: text, perm: text, sign: item):
    set {-nowSetAb} to {_perm}
    set {-ab.%{-nowSetAb}%::name} to {_name}
    set {-ab.%{-nowSetAb}%::perm} to {_perm}
    set {-ab.%{-nowSetAb}%.sign} to {_sign}

function setCast(c: text):
    set {-ab.%{-nowSetAb}%::cast} to {_c}

function setEffects(e: texts):
    set {-ab.%{-nowSetAb}%.effects::*} to {_e::*}

function setCd(c: text):
    set {-ab.%{-nowSetAb}%::cd} to {_c}

function regAb(basic: boolean = false):
    if {_basic} is true:
        add {-ab.%{-nowSetAb}%::perm} to {-abBasicList::*}
    else:
        add {-ab.%{-nowSetAb}%::perm} to {-abList::*}
    clear {-nowSetAb}

on load:
    clear {-abBasicList::*}
    clear {-abList::*}
#===基礎===
    abInit("&3神祕的加護", "MysteriousBless", end portal frame)
    setCast("死亡時")
    setEffects(("自身工具欄內第一格與第二格", "的物品不會掉落" and "（可避免消失詛咒）", "可透過升級使加護格數增加"))
    setCd("無")
    regAb(true)
#=== ===
    abInit("&3盾牌衝鋒", "ShieldAssault.Basic", shield)
    setCast("於衝刺時拿著&3盾牌&7攻擊生物")
    setEffects(("將遭攻擊生物撞飛衝刺的位移，", "並根據位移增加額外傷害", "（衝刺2格 = 一顆心" and "最多額外增加2顆心）"))
    setCd("無")
    regAb(true)
#===能力===
    abInit("&c爆裂魔法！", "Explosion", tnt)
    setCast("在訊息框輸入 &cExplosion!&r")
    setEffects(("以自身為中心製造出一個" and "威力10的爆炸，自身免疫該傷害"))
    setCd("10分鐘")
    regAb()
#=== ===
    abInit("&3魚老師", "FishSensei", raw fish)
    setCast("拿著不同種類的&3魚")
    setEffects(("&3普通魚：", "- 飢餓度數值不會改變", "- （不會增加也不會減少）", "&3鮭魚：", "- 攻擊時使被攻擊者往上飛8格", "&3河豚：", "- 攻擊時造成中毒I 5秒與噁心 10秒", "&3小丑魚：" and "- 攻擊時造成發光 30秒"))
    setCd("無")
    regAb()
#=== ===
    abInit("&9查洛伊藥劑", "Charoi", potion of night vision)
    setCast("自另一手切換任意一瓶&9水瓶&7至主手")
    setEffects(("立即回復3格飽食並獲得", "&6回復II 速度III 抗性II &7與 &6夜視" and "狀態12.5秒，此期間無法攻擊"))
    setCd("6分鐘")
    regAb()
#=== ===
    abInit("&a一定到！", "WillBeThere", eye of ender)
    setCast("自另一手切換&a終界之眼&7至主手")
    setEffects(("隨機傳送到一名其他線上玩家旁邊" and "沒有時即發動失敗，不計使用"))
    setCd("5分鐘")
    regAb()
#=== ===
    abInit("&3盾牌衝鋒&7［&5加強&7］&r", "ShieldAssault.Enhanced", shield)
    setCast("於衝刺時拿著&3盾牌&7攻擊生物")
    setEffects(("將攻擊者面前周圍3格之", "所有生物撞飛所衝刺的位移，", "並根據位移增加額外傷害", "（衝刺1.5格 = 一顆心" and "最多額外增加4.5顆心）"))
    setCd("無")
    regAb()
#=== ===
    abInit("&9吾主在此", "LuminositeEternelle", cyan banner item)
    setCast("在蹲下時放置上&9旗幟&r")
    setEffects(("以旗幟為中心點", "範圍10格內的玩家獲得", "&6- 生命值提升 I（已回復） 30秒", "&6- 抗性 I 30秒", "&6- 吸收 II 10秒", "&6- 回復 I 10秒", "發動者額外獲得" and "&9- 緩速 VII 30秒"))
    setCd("10分鐘")
    regAb()
#=== ===
    abInit("&6雞雞攻擊！", "ChickenAttack", egg)
    setCast("對生物丟出&6雞蛋&r")
    setEffects(("可以造成 4 顆心的傷害"))
    setCd("無")
    regAb()
#=== ===
    abInit("&b客家人", "Hakka", spawn egg)
    setCast("在訊息框輸入 &bHakka!&r")
    setEffects(("隨機變成一種生物3分鐘，" and "僅限外觀，可迷惑怪物"))
    setCd("10分鐘")
    regAb()
#=== ===
    abInit("&b雨男", "Blackmore", lily pad)
    setCast("天氣轉換為下雨或雷雨時")
    setEffects(("獲得飛行能力" and "於天晴時即消失"))
    setCd("無")
    regAb()
#=== ===
    abInit("&5原住民", "NativeHunter", leather)
    setCast("非玩家生物死亡時")
    setEffects(("以 18％ 的機率", "獲取雙倍的掉落物" and "（可與掠奪附魔疊加）"))
    setCd("無")
    regAb()
#=== ===
    abInit("&6一撮火藥", "LittleFlour", gunpowder)
    setCast("持有&c火藥&7時丟棄&6烈焰桿&7")
    setEffects(("從身上扣除1個火藥", "並以速度2射出一個地獄幽靈火球" and "（成功時烈焰桿不會被丟棄）"))
    setCd("無")
    regAb()
#=== ===
    abInit("&e攻擊轉換", "ATKTrade", golden sword)
    setCast("常駐")
    setEffects(("所有造成的傷害 +25％", "所有受到的傷害 +40％" and "（傷害計算為無條件捨去）"))
    setCd("無")
    regAb()